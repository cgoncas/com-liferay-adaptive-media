buildscript {
	dependencies {
		classpath group: "com.liferay", name: "com.liferay.gradle.plugins.defaults", version: "latest.release"
		classpath group: "org.kt3k.gradle.plugin", name: "coveralls-gradle-plugin", version: "2.6.3"
	}

	repositories {
		mavenLocal()

		maven {
			url "https://cdn.lfrs.sl/repository.liferay.com/nexus/content/groups/public"
		}

		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
}

apply plugin: "com.github.kt3k.coveralls"
apply plugin: "com.liferay.app.defaults.plugin"
apply plugin: "jacoco"

task createTempDir(type: Exec) {
	commandLine 'mkdir', '-m', '777', '-p', "${rootDir}/build/tmpdir"
}

task runDocker(type: Exec) {
	dependsOn createTempDir

	def javaOpts = project.getProperty('javaOpts');

	if (project.hasProperty('debug')) {
		javaOpts += ' ' + project.getProperty('debugOpts');
	}

	commandLine 'docker', 'run',
			'-h', "localhost",
			'-e', "JAVA_OPTS=$javaOpts",
			'-p', '5005:5005',
			'-p', '8080:8080',
			'-p', '8099:8099',
			'-p', '11311:11311',
			'-v', "${rootDir}/../bundles/deploy:/liferay/deploy",
			'-v', "${rootDir}/docker/portal-ext.properties:/liferay/portal-ext.properties",
			'-v', "${rootDir}/build/tmpdir:${rootDir}/build/tmpdir",
			'-d',
			'--name', "com-liferay-adaptive-media",
			'liferay/liferay-de:20170203072957391859963-db'
}

configure(subprojects.findAll {!it.childProjects}) {
	apply plugin: "com.liferay.defaults.plugin"
	apply plugin: "jacoco"

	configurations {
		runtimeLib {
			description = 'deployable runtime libs'
		}

		compile.extendsFrom(runtimeLib)
	}

	liferay {
		deployDir = new File("${rootDir}/../bundles/deploy")
	}

	jacocoTestReport {
		additionalSourceDirs = files(sourceSets.main.allSource.srcDirs)
		sourceDirectories = files(sourceSets.main.allSource.srcDirs)
		classDirectories =  files(sourceSets.main.output)

		reports {
			html.enabled = true
			xml.enabled = true
		}
	}

	test {
		ignoreFailures = false
	}

	testIntegration {
		dependsOn createTempDir

		ignoreFailures = false
		systemProperty "java.io.tmpdir", "${rootDir}/build/tmpdir"
	}

	task deployLibs(type: Copy) {
		from "${projectDir}/libs"
		exclude '*-javadoc.jar'
		exclude '*-sources.jar'
		into liferay.deployDir
		rename { String fileName ->
			fileName.replaceAll("(.*)-((\\d\\.{1})*\\d)\\.jar", "\$1.jar")
		}
	}

	task deployRuntimeLibs(type: Copy) {
		into liferay.deployDir
		from configurations.runtimeLib
		rename { String fileName ->
			fileName.replaceAll("(.*)-((\\d\\.{1})*\\d)\\.jar", "\$1.jar")
		}
	}

	runDocker.dependsOn deploy
	runDocker.dependsOn deployLibs
	runDocker.dependsOn deployRuntimeLibs
}

repositories {
	mavenLocal()
	jcenter()
}

task coverageReport(type: org.gradle.testing.jacoco.tasks.JacocoReport) {
	additionalSourceDirs = files(subprojects.findAll{!it.childProjects}.sourceSets.main.allSource.srcDirs)
	classDirectories = files(subprojects.findAll{!it.childProjects}.sourceSets.main.output)
	executionData = files(subprojects.findAll{!it.childProjects}.jacocoTestReport.executionData)
	sourceDirectories = files(subprojects.findAll{!it.childProjects}.sourceSets.main.allSource.srcDirs)

	reports {
		html.enabled = true
		xml.enabled = true
	}

	onlyIf = { true }

	doFirst {
		executionData = files(executionData.findAll { it.exists() })
	}
}

tasks.coveralls {
	dependsOn 'coverageReport'
}

coveralls.jacocoReportPath = "build/reports/jacoco/coverageReport/coverageReport.xml"
coveralls.sourceDirs = files(subprojects.findAll{!it.childProjects}.sourceSets.main.allSource.srcDirs).collect {
	file -> file.absolutePath
}